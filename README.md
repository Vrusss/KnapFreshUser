# Расширение для создания условного пользователя для обмена (в разработке)

Расширение написано на Бухгалтерия предприятия, редакция 3.0 (3.0.173.23) для работы в следующих системах:
Файловая, "1С Фреш", "42 Cloud"

Предназначение расширения - создание, через общую форму, условного пользователя для обмена, который будет иметь полные права и
ему будет запрещено заходить в режим Предприятия. Также, нужно не пускать в режим предприятия предустановленных клиентом пользователей,
из систем "1С Фреш" и "42 Cloud" 

Примечание: 
- Для хранения данных имени созданного условного пользователя используется регистр сведений БезопасноеХранилищеОбластейДанных
- Автор не имеет права загрузить файл расширения в репозиторий, т.к. разработка ведется в расширении клиента, где находятся его собственные разработки

# Knap_new_test

## бит_ОбщийМодульУП
	// Ищет в регистре сведений БезопасноеХранилищеДанныхОбластейДанных имя условного пользователя
 	// если находит, то возвращает имя, если нет, то Неопределено
 	// Возвращаемаое значение:
  	//	- Строка
   	//	- Неопределено
	Функция ПолучитьИмяУП() Экспорт // ++ Мальцев В.К. PBSD-5482
	
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	БезопасноеХранилищеДанныхОбластейДанных.Данные КАК Данные
			|ИЗ
			|	РегистрСведений.БезопасноеХранилищеДанныхОбластейДанных КАК БезопасноеХранилищеДанныхОбластейДанных
			|ГДЕ
			|	БезопасноеХранилищеДанныхОбластейДанных.Владелец = &Владелец";
		
		Запрос.УстановитьПараметр("Владелец", "бит_УсловныйПользователь");
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			Данные = Выборка.Данные.Получить();
			Имя = Данные.Имя
		Иначе
			Имя = Неопределено
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Возврат Имя;
		
	КонецФункции // -- Мальцев В.К. PBSD-5482 
	// Проверяет наличиче у текущего пользоваетля полных прав
 	// Возвращаемое значение:
  	//	Булево
	функция ПолучитьПраваТекущегоПользователя() Экспорт
		Пользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
		
		Если Не ЗначениеЗаполнено(Пользователь.Имя) Тогда
			Возврат Истина;
		КонецЕсли;
		
		ПраваИмеются = Пользователь.Роли.Содержит(Метаданные.Роли["ПолныеПрава"]);
		
		Возврат ПраваИмеются;
	КонецФункции

## Модуль приложения

	// Проверяет, по имени, можно ли пустить текущего пользователя в режим Предприятия, если у него есть полные права, 
	&После("ПриНачалеРаботыСистемы")
	Процедура бит_ПриНачалеРаботыСистемы() // ++ Мальцев В.К. PBSD-5482
		
		Если бит_ОбщийМодульУП.ПолучитьПраваТекущегоПользователя() Тогда
			ТекущийПользователь = ИмяПользователя();
			УсловныйПользователь = бит_ОбщийМодульУП.ПолучитьИмяУП();
					
			Если ТекущийПользователь = "knapfresh" ИЛИ ТекущийПользователь = "42Cloud" 
				ИЛИ ТекущийПользователь = УсловныйПользователь Тогда
				ЗавершитьРаботуСистемы(Ложь, ); 
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры // -- Мальцев В.К. PBSD-548
 ## бит_Форма
 
	#Область КомандыФормы
	// Команда кнопки "Создать пользователя". Проверяет заполненность и корректность заполнения полей формы
	&НаКлиенте
	Процедура СоздатьПользователя(Команда) // ++ Мальцев В.К. PBSD-5482
		
		Если Не ПроверитьЗаполнение() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Имя пользователя не может быть пустым";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
		Если Пароль <> ПодтверждениеПароля Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Пароли не совпадают";
			Сообщение.Сообщить();                 
			Возврат;             
		КонецЕсли;
			
		Результат = СоздатьПользователяНаСервере();
		
		Если Результат = Ложь Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Пользователь с именем ""%1"" уже существует", Имя);
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482
	// Команда кнопки "Настройки" -> "Открыть настройки хранения версий"
	&НаКлиенте
	Процедура ОткрытьВерсионирование(Команда) // ++ Мальцев В.К. PBSD-5482
		
		ОткрытьФормуВерсионирования();	
		
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482
	
	#КонецОбласти
	
	#Область СлужебныеПроцедурыИФункции
	// Проверяет по имени существует ли создаваемый пользователь и останавливает дальнейшее выполнение программы если существует
 	// Возвращаемое значение:
  	//	Булево
	&НаСервере
	Функция СоздатьПользователяНаСервере() // ++ Мальцев В.К. PBSD-5482
		
		УстановитьПривилегированныйРежим(Истина);
		Если ПользователиИнформационнойБазы.НайтиПоИмени(СтрЗаменить(Имя, " ", "")) <> Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
		СоздатьУсловногоПользователя();
		Возврат Истина;
		
	КонецФункции // -- Мальцев В.К. PBSD-5482
	// Создает служебного пользователя с указанными на форме данными, с полными правами и запретом на вход в режим предприятия, 
 	// затем записывает имя пользователя в регистр сведений БезопасноеХранилищеДанныхОбластейДанных
	&НаСервере
	Процедура СоздатьУсловногоПользователя() Экспорт // ++ Мальцев В.К. PBSD-5482
		
		ОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
		ОписаниеПользователяИБ.Имя = СтрЗаменить(Имя, " ", "");
		ОписаниеПользователяИБ.ПолноеИмя = Имя;
		
		Если Имя = "knapfresh" Или Имя = "42Cloud" Тогда 
			ОписаниеПользователяИБ.АдресЭлектроннойПочты = "***@****.**";
		КонецЕсли;
		
		ОписаниеПользователяИБ.АутентификацияСтандартная = Истина;
		ОписаниеПользователяИБ.ПоказыватьВСпискеВыбора = Ложь;
		ОписаниеПользователяИБ.Вставить("Действие", "Записать");
		ОписаниеПользователяИБ.Вставить("ВходВПрограммуРазрешен", Истина);
		ОписаниеПользователяИБ.ЗапрещеноИзменятьПароль = Истина;
		ОписаниеПользователяИБ.Пароль = Пароль;
		ОписаниеПользователяИБ.Роли = Новый Массив;
		ОписаниеПользователяИБ.Роли.Добавить("ПолныеПрава");
		НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
		НовыйПользователь.Наименование = ОписаниеПользователяИБ.ПолноеИмя;
		НовыйПользователь.Служебный = Истина;
		НовыйПользователь.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
		Попытка 
			НовыйПользователь.Записать();
			УстановитьПривилегированныйРежим(Истина);
			ЗаписатьВОбластьДанных("бит_УсловныйПользователь", ОписаниеПользователяИБ.Имя, "Имя");
			УстановитьПривилегированныйРежим(Ложь);
			Сообщить("Пользователь успешно записан!");
		
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка создания нового пользователя: " + ОписаниеОшибки());
			ЗаписьЖурналаРегистрации(СтрШаблон("Создание пользователя ""%1""", Имя),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482 
	// Записывает или перезаписывает элемент регистра сведений БезопасноеХранилищеДанныхОбластейДанных
 	// Параметры:
	&НаСервере
	Процедура ЗаписатьВОбластьДанных(Владелец, Данные, Ключ = "Пароль") // ++ Мальцев В.К. PBSD-5482
		
		БезопасноеХранилищеОД = РегистрыСведений.БезопасноеХранилищеДанныхОбластейДанных.СоздатьМенеджерЗаписи();
		
		БезопасноеХранилищеОД.Владелец = Владелец;
		БезопасноеХранилищеОД.Прочитать();
		
		Если БезопасноеХранилищеОД.Выбран() Тогда
			
			ДанныеДляСохранения = БезопасноеХранилищеОД.Данные.Получить();
			
			Если ТипЗнч(ДанныеДляСохранения) <> Тип("Структура") Тогда
				ДанныеДляСохранения = Новый Структура();
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Ключ) Тогда
				ДанныеДляСохранения.Вставить(Ключ, Данные);
			Иначе
				ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеДляСохранения, Данные, Истина);
			КонецЕсли;
			
			ДанныеДляХранилищеЗначения = Новый ХранилищеЗначения(ДанныеДляСохранения, Новый СжатиеДанных(6));
			БезопасноеХранилищеОД.Данные = ДанныеДляХранилищеЗначения;
			БезопасноеХранилищеОД.Записать();
			
		Иначе
			
			ДанныеДляСохранения = ?(ЗначениеЗаполнено(Ключ), Новый Структура(Ключ, Данные), Данные);
			ДанныеДляХранилищеЗначения = Новый ХранилищеЗначения(ДанныеДляСохранения, Новый СжатиеДанных(6));
			
			БезопасноеХранилищеОД.Данные = ДанныеДляХранилищеЗначения;
			БезопасноеХранилищеОД.Владелец = Владелец;
			БезопасноеХранилищеОД.Записать();
			
		КонецЕсли;
		
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482
	  
	&НаСервере
	Процедура ИзменитьВариантВерсионирования() // ++ Мальцев В.К. PBSD-5482
		
		Объекты = Новый Соответствие;
		Объекты.Вставить("Документ.ПоступлениеТоваровУслуг","");
		Объекты.Вставить("Документ.СчетФактураПолученный","");
		Объекты.Вставить("Документ.СчетФактураВыданный","");
		Объекты.Вставить("Документ.РеализацияТоваровУслуг","");
		Объекты.Вставить("Документ.АвансовыйОтчет","");
		
		ВерсионированиеОбъектов.ВключитьВерсионированиеОбъектов(Объекты);
		
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482
	
	&НаКлиенте
	Процедура ОткрытьФормуВерсионирования() // ++ Мальцев В.К. PBSD-5482
		
		ОткрытьФорму("РегистрСведений.НастройкиВерсионированияОбъектов.Форма.НастройкиХраненияИстории");
					
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482
	
	#КонецОбласти
	
	#Область СобытияФормы
	
	&НаСервере
	Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) // ++ Мальцев В.К. PBSD-5482
		
		Если Не бит_ОбщийМодульУП.ПолучитьПраваТекущегоПользователя() Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Эта функция доступно только пользователям с полнами правами";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482
	
	&НаКлиенте
	Процедура ПриОткрытии(Отказ) // ++ Мальцев В.К. PBSD-5482
			
		Текст = "Изменить режим сохранения версий на ""При записи"" для следующих документов: Поступление (акты, накладные, УПД), Реализация (акты, накладные, УПД), 
		|Счет-фактура полученный, Счет-фактура выданный?";
		Режим = РежимДиалогаВопрос.ДаНет;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтаФорма), Текст, Режим, 0);
	
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриОткрытииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Ответ = РезультатВопроса;
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ИзменитьВариантВерсионирования();
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
				МодульВерсионированиеОбъектовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ВерсионированиеОбъектовКлиент");
				МодульВерсионированиеОбъектовКлиент.ПриИзмененииФлажкаХранитьИсторию(Истина);
			КонецЕсли;
		КонецЕсли;
		
		ОткрытьФормуВерсионирования();
	
	КонецПроцедуры // -- Мальцев В.К. PBSD-5482
	
	#КонецОбласти
